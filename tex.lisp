;;;; Render Geneva document as TeX manuscript.

;;; Expects macros as implicated below:
;;;
;;; Text:
;;; \genbold{#1} \genitalic{#1} \genfixedwidth{#1} \genurl{#1}
;;; E.g.: \genbold{...} ...
;;;
;;; \gentinyparagraph{#1} (For very short paragraphs)
;;;; E.g. \gentinyparagraph{...} 
;;;
;;; Listing:
;;; \genlisting{#1} \genitem{#1}
;;; E.g.: \genlisting{\genitem{...} ...}
;;;
;;; Table:
;;; \gentable{#1}{#2}{#3} \genhead{#1} \genrow{#1} \gencolumn{#1}
;;; E.g.: \gentable{Description...}{XX...}{
;;;         \genrow{\genhead{...} \gencolsep... \genhead{...}}
;;;         \genrow{\gencolumn{...} \gencolsep \gencolumn{...}...}
;;;         ...
;;;       }
;;;
;;; Figures:
;;; \gengraphic{#1}{#2}
;;; \genfallbackfigure{#1}{#2}
;;; \genverbatimstart
;;; \genverbatimend
;;; \genverbatimdescription{#1}
;;; E.g.:
;;;  (Graphic figure)   \gengraphic{...}{<URL>}
;;;  (Fallback figure)  \genfallbackfigure{...}{<URL>}
;;;  (Plaintext figure) \genverbatimstart ... \genverbatimend
;;;                     \genverbatimdescription{...}
;;;
;;; Sections:
;;; \gensection{#1} \gensubsection{#1} \gensubsubsection{#1}
;;; E.g.: \gensection{...} ...

(defpackage geneva.tex
  (:documentation
   "Render Geneva document as TeX manuscript.")
  (:use :cl
	:geneva
	:texp
        :file-types
        :named-readtables)
  (:export :render-tex))

(in-package :geneva.tex)

(in-readtable texp:syntax)

(defvar *section-level* 0
  "Section level.")

(defun render-text-token (text-token)
  "Render TEXT-TOKEN as TeX using macro calls for markup tokens."
  (ecase (content-type text-token)
    (:plain       (write-string #1=(escape (content-values text-token))))
    (:bold        (tex (genbold       {($ #1#)})))
    (:italic      (tex (genitalic     {($ #1#)})))
    (:fixed-width (tex (genfixedwidth {($ #1#)})))
    (:url         (tex (genurl        {($ #1#)})))))

(defun render-text (text)
  "Render TEXT in TeX representation."
  (dolist (text-token text)
    (render-text-token text-token))
  (values))

(defun text-length (text)
  "Number of characters in TEXT."
  (loop for token in text sum (length (content-values token))))

(defun tiny-paragraph-p (paragraph)
  "If PARAGRAPH contains less than 128 characters its a _tiny
paragraph_."
  (< (text-length (content-values paragraph)) 128))

(defun render-paragraph (paragraph)
  "Render PARAGRAPH in TeX representation."
  (if (tiny-paragraph-p paragraph)
      (tex (gentinyparagraph
            {($ (render-text (content-values paragraph)))}))
      (tex ($ (render-text (content-values paragraph)))
           (br))))

(defun render-listing (listing)
  "Render LISTING in TeX representation."
  (tex (genlisting
	{($ (dolist (item (content-values listing))
	      (tex (genitem {($ (render-text item))}))))})
       (br)))

(defun render-table-row (row &optional (type :column))
  "Render ROW in TeX representation."
  (loop for head = row then (cdr head)
        for column = (car head)
        while head do
       (ecase type
         (:column (tex (gencolumn {($ (render-text column))})))
         (:head   (tex (genhead {($ (render-text column))}))))
       (when (cdr head)
         (tex (gencolsep)))))

(defun table-format (rows)
  "Compute table format for ROWS."
  (let* ((thresh 16)
         (n (loop for row in rows maximize (length row)))
         (cs (loop for i from 0 to (1- n)
                   for max = (loop for row in rows
                                maximize (text-length (nth i row)))
                if (> max thresh) collect max
                else collect thresh))
         (sum (loop for c in cs sum c))
         (rs (loop for c in cs
                collect (float (* (/ c sum) n)))))
    (format nil "钧苘梵辁妣彳荑箝妪佝蝮┅ㄤ彐躅蝈钿弪翎忪翎忪濠⒁孱溴粤绿轭藻蝈痱弩孱翎糸镱眭祠轲戾鲠祯瀛忾钿ㄤ弩泸轲糸镱蝻黧ㄣ镱翦铘鲠祯弩翎忪濠翦ㄧ孱翎忪à蝈钿弪翦溴筱蜷痿轱瞟à翎忪瀛骘蝽狒蝻黧┅ㄧ孱蝻à蝈钿弪翎忪瀛蝻ㄦ轵篝蝻黧鸿遽洎àㄤ镬轶蝻蝈篝蝻黧┅翦ㄧ孱蝻à蝈钿弪翎忪瀛蝻蝻鳗┅┅ㄢ颟┅ㄤ彐躅蝈钿弪礤溟礤溟岘镡赍泗⒁孱溴团纳轭藻蝈痱弩孱翎糸镱眭祠轲戾鲠祯瀛忾钿ㄤ弩泸轲糸镱躜飑ㄣ镱翦铘鲠祯弩礤溟岘镡赍泗ㄩㄦ殪瀛翎珞躜洪磲珏翦ㄧ孱珧狃栝à蝈钿弪翦溴筱蜷痿轱瞟àㄥ筱狃躜飑ㄢ颟翦ㄧ孱驷祆忉汶骈珲蝈à蝈钿弪翦溴筱蜷痿轱瞟àㄥ筱狃躜飑ㄢ颟┅┅ㄤ彐躅蝈钿弪痨衢铘屮痨衢铘屮舡镡赍泗⒁孱溴刑辽卧咆原下逝迷轭藻蝈痱弩孱翎糸镱眭祠轲戾鲠祯瀛忾钿ㄤ弩泸轲糸镱翦舂ㄣ镱翦铘鲠祯弩痨衢铘屮舡镡赍泗翦ㄧ孱鲥蜮狒轫篝狎舂àㄦ蝈箬扉铄┅àㄥ筱狃翦舂àㄦ蝈箬扉铄┅ㄧ孱鲥蜮狒轫孱洎ㄧ孱鲥蜮狒轫溴筱蜷痿轱à蝈钿弪翦溴筱蜷痿轱瞟ㄢ颟┅ㄤ彐躅蝈钿弪桢徜弪ㄨ遽溴颟⒁孱溴扰聊乓轭藻蝈痱弩孱翎糸镱ㄣ狍箦泗轱瞽戾鲥飒ò翦ㄧ孱箦泗轱à蝈钿弪翦桢徜弪┅┅ū翦ㄧ孱篚怏邈糸镱à蝈钿弪翦桢徜弪┅┅雉桢蝼轶翦ㄧ孱篚怏踱箦泗轱à蝈钿弪翦桢徜弪┅┅┅翦ㄢ颟┅ㄤ彐躅蝈钿弪箦泗轱箦泗轱瞟⒁孱溴优迷上轭藻蝈痱弩孱翎糸镱眭祠轲戾鲠祯瀛忾钿ㄨ遽溴泔铘孱趔ㄣ镱翦铘鲠祯弩箦泗轱瞟蝈钿弪桢徜弪桢徜弪戾è箦泗轱瞽戾鲥飒ū箦泗轱瞽戾鲥飒┅蝈钿弪泔铘孱趔泔铘孱趔┅翦ㄢ颟┅ㄤ彐躅蝈钿弪泔铘孱ㄣ镱翦铘⒁孱溴孟卧盼轭梏盱蝈痱弩孱翎糸镱ㄣ狍ㄣ镱翦铘豉疱泔铘孱舂ê疳蜥珧狃蝈钿弪疳蜥珧狃泔铘孱舂ê扉篝轭蝈钿弪扉篝轭泔铘孱舂ê翎忪蝈钿弪翎忪泔铘孱舂ê礤溟蝈钿弪礤溟泔铘孱舂ê痨衢铘屮蝈钿弪痨衢铘屮泔铘孱舂ê箦泗轱蝈钿弪箦泗轱泔铘孱舂ㄥ蝌矧⑸铞犰殇泔铘孱豉疱轭孟卧盼院赢ㄣ镱翦铘豉疱泔铘孱舂┅┅ㄤ彐躅蝈钿弪泔铘孱趔ㄣ镱翦铘螬⒁孱溴滹沲礤铘矧箦泗轱孟卧盼杂轭藻蝈痱弩孱翎糸镱ㄤ镬轶ㄣ镱翦铘泔铘孱趔蝈钿弪泔铘孱泔铘孱舂┅ㄤ彐躅蝈钿弪翦ㄤ镢蹴孱脲篝蝈犴篝犷溽蜾秕麴豸箦泗轱瞽戾鲥箦泗轱瞽戾鲥飒┅⒁孱溴清铄鲠南谜团卧狍藻磲铛筱蜷痿麸釉遗镣戾è篝犷溽蜾秕麴豸篝蝈犴í箦泗轱瞽戾鲥飒箦泗轱瞽戾鲥飑蝈钿弪泔铘孱趔滹沲礤铘┅