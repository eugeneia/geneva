;;;; MPC grammar used by GENEVA.MK2.

(in-package :geneva.mk2)

(defun =escape ()
  (=character *escape-directive*))

(defun =escaped-char ()
  (=and (=escape) (=item)))

(defun =plain-char ()
  (=or (=escaped-char)
       (=item)))

(defun =plain-text (until)
  (=string-of (=unless until (=plain-char))))

(defun =token (char)
  (=unless (=escaped-char)
           (=character char)))

(defun =markup% (constructor start &optional (end start))
  (=let* ((_ (=token start))
          (text (=or (=plain-text (=token end)) (=result "")))
          (_ (=token end)))
    (=result (funcall constructor text))))

(defun =markup ()
  (=or (=markup% #'make-bold
                 *bold-directive*)
       (=markup% #'make-italic
                 *italic-directive*)
       (=markup% #'make-fixed-width
                 *fixed-width-directive-start*
                 *fixed-width-directive-end*)
       (=markup% #'make-url
                 *url-directive-start*
                 *url-directive-end*)))

(defun =markup-directive ()
  (=one-of *markup-directives*))

(defun =text (until)
  (=zero-or-more
   (=or (=markup)
        (=plain-text (=or (=markup-directive)
                          until)))))

(defun =newline* ()
  (=and (=zero-or-more (=unless (=newline) (=whitespace)))
        (=newline)))

(defun =double-newline ()
  (=and (=newline*)
        (=newline*)))

(defun =end-of-document ()
  (=skip-whitespace (=end-of-input)))

(defun =content-delimiter ()
  (=or (=double-newline)
       (=end-of-document)))

(defun =paragraph ()
  (=let* ((text (=text (=or (=content-delimiter)
                            (=token *section-end*))))
          (_ (=content-delimiter)))
    (if text
        (=result (make-paragraph text))
        (=fail))))

(defun =list-item ()
  (=prog2 (=token *listing-item*)
          (=text (=or (=token *listing-item*)
                      (=content-delimiter)))))

(defun =listing ()
  (=let* ((items (=one-or-more (=list-item)))
          (_ (=content-delimiter)))
    (=result (make-listing items))))

(defun =object% (keyword constructor parser)
  (let ((delimiter (=token *object-delimiter*)))
    ;; DELIMITER KEYWORD TEXT DELIMITER PARSER.
    (=let* ((_ (=and delimiter (=string keyword nil)))
            (description (=text (=or delimiter (=content-delimiter))))
            (_ (=or (=and delimiter (=newline*))
                    ;; Description is not terminated properly
                    (=syntax-error 'malformed-object)))
            (body parser))
      (=result (funcall constructor description body)))))

(defun =skip-horizontal-space (parser)
  "Skip horizontal whitespace and apply PARSER."
  (=and (=zero-or-more (=unless (=newline) (=whitespace)))
        parser))

(defun =url ()
  "We are liberal as to whats a valid URL. That decision is out of scope.
We even allow multiline strings with escaped newlines."
  (=prog1 (=skip-horizontal-space (=string-of (=not (=token #\Newline))))
          (=or (=content-delimiter)
               ;; Object is not terminated properly
               (=syntax-error 'malformed-object))))

(defun =table-column ()
  (=and (=token *table-item*)
        (=text (=or (=token *table-item*)
                    (=newline*)))))

(defun =table-row ()
  (=one-or-more
   (=skip-horizontal-space (=table-column))))

(defun =table-body ()
  (=prog1 (=one-or-more (=prog1 (=table-row)
                                (=maybe (=newline*))))
          (=or (=content-delimiter)
               ;; Single newline is ok in case last row ate one.
               (=newline*)
               ;; Object is not terminated properly
               (=syntax-error 'malformed-object))))

(defun =plaintext-terminator ()
  (=and (=skip-whitespace (=token *object-delimiter*))
        (=content-delimiter)))

(defun =plaintext-line ()
  (=unless (=or (=plaintext-terminator)
                (=end-of-document))
           (=line)))

(defun =plaintext-body ()
  (=let* ((lines (=zero-or-more (=plaintext-line)))
          (_ (=or (=plaintext-terminator)
                  (=syntax-error 'malformed-object))))
    (=result (format nil "狺扉铄螬┅ㄤ彐躅斤怅邈ī斤斤怅邈籁礤溟岘脲黠蜾＇磲脲礤溟锦蜢┅斤怅邈籁翎忪瀛脲黠蜾＇磲脲翎忪紧徕戾怙澌┅斤怅邈籁痨衢铘屮舡脲黠蜾＇磲脲痨衢铘屮金灬轭翦舡怙澌┅┅ㄤ彐躅襟邈糸镱ī借犷潇弪汜箦届弭è紧镫孱箦泗轱瞽篝狎舄┅ㄨ遽溴紧屮姐镱翦铘溴扉黹翦颟┅ㄟ姐镱翦铘溴扉黹翦颟ㄣ镱翦铘姐镱翦铘螬ㄟ斤襟腴瓠麒轸弩疳沐紧镫孱箦泗轱瞽孱洫┅换渝泗轱铙眭篝忮沆矬邃狴瀹襟铘狲弪蝻箦泗轱瞽铒舡沆矬邃┅┅津弩蹯磲脲箦泗轱桢徜弪泔铘孱趔┅换族栳鲥犷躅沆矬邃箦泗轱箫麇箝珙犰麒弪轸镳孱邃箦泗轱瞽铒舡沆矬邃ī襟铘狲弪蝻э疱瞽箦泗轱瞟┅ㄤ彐躅姐镱翦铘ī晋弪锃矧盹蝈换体徜轭麒轸弩疳沐轶轭箝珙殒殂犷舢襟腴瓠麒轸弩疳沐斤襟邈糸镱斤怅邈舂届轶糸铉金狎徵蜥痂┅┅ㄤ彐躅戒镢蹴孱ī金蝻绫姐镱翦铘螬斤藉钿镦滹沲礤铘换疹戾篌犰轭瘐麽篚沣弩箧蹯禊疳蝮邃箫礤翳轭麇铘黩镱绠襟铘狲弪蝻躅蝈泔珙辁邃轭瘐舂┅